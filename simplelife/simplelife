1 gosub 10
2 poke 646,1: poke 53280,0: poke 53281,0
3 c=0: b=0: y=18: bfg=0
4 car=0: sex=0: beer=0: bnk=0: gun=0
5 goto 400
10 rem clear screen
11 print chr$(147): return
20 rem character positioning and printing
21 poke 211,sp: poke 214,ze: sys 58640: print a$: return
30 rem sleep
31 goto 33
32 t0=ti: for t=t0+t*60 to t: t=ti: t=t-5184e3*(t<t0): next: return
33 t=4: gosub 32: return
40 rem title creator
41 gosub 10
42 print t$: print: return
50 rem press any key to continue
51 gosub 30
52 print: print: print "press any key to continue..."
53 for w=-1 to 0: get a$: w=a$="": next
60 rem random chance generator
61 ch = int(rnd(0)*100): return
70 rem menu creator
71 for i=1 to m: print i"- "; m$(i): next: return
100 rem ager
101 y=x+y
102 if y>80 then 7520
103 return
400 rem intro
401 sp=14: ze=10: a$="simple life": gosub 20
402 sp=13: ze=11: a$="v0.4.0 (alpha)": gosub 20
403 sp=12: ze=13: a$="ported from t-84": gosub 20
404 sp=15: ze=14: a$="to the c64": gosub 20: gosub 30
405 gosub 10
406 sp=16: ze=12: a$="halogod": gosub 20: gosub 30
407 sp=16: ze=12: a$="silence": gosub 20: gosub 30
450 rem object of game
451 gosub 10
452 sp=8: ze=10: a$="object is to get rich,": gosub 20: gosub 30
453 sp=12: ze=11: a$="buy lots of crap,": gosub 20: gosub 30
454 sp=4: ze=12: a$="and be the best person": gosub 20
455 sp=14: ze=13: a$="you can possibly be": gosub 20: gosub 30
500 rem main menu
510 g=0
520 t$="simple life": gosub 40
521 m$(1)="home"
522 m$(2)="bank"
523 m$(3)="car shop"
524 m$(4)="bar"
525 m$(5)="your car"
526 m$(6)="stats"
527 m$(7)="quit"
528 m=7: gosub 70
550 print: input "where to";i
551 if i=1 then 1000
552 if i=2 then 2000
553 if i=3 then 3000
554 if i=4 then 4000
555 if i=5 then 5000
556 if i=6 then 6000
557 if i=7 then 7000
1000 rem home
1020 t$="home": gosub 40
1021 m$(1)="shower"
1022 m$(2)="sleep"
1023 m$(3)="computer"
1024 m$(4)="leave"
1025 m=4: gosub 70
1050 print: input "what now";i
1051 if i=1 then 1100
1052 if i=2 then 1200
1053 if i=3 then 1300
1054 if i=4 then 500
1100 rem shower
1110 gosub 60: if ch<3 then 7540
1111 t=int(rnd(0)*35+15)
1112 x=t/5.256e5: gosub 100
1120 t$="shower": gosub 40
1121 print "you shower for"t"minutes"
1122 gosub 30: goto 1000
1200 rem sleep
1210 t=int(rnd(0)*2+2)
1211 x=t/8760: gosub 100
1220 t$="sleep": gosub 40
1221 print "you sleep for"t"hours"
1222 gosub 30: goto 1000
1300 rem computer
1320 t$="computer": gosub 40
1321 m$(1)="games"
1322 m$(2)="porn"
1323 m$(3)="shut down"
1324 m=3: gosub 70
1350 print: input "what now";i
1351 if i=1 then 1360
1352 if i=2 then 1370
1353 if i=3 then 1000
1360 rem games
1361 x=2/8760: gosub 100
1362 t$="video games": gosub 40
1363 print "you get"int(rnd(0)*155)+5"headshots"
1364 gosub 30: goto 1300
1370 rem porn
1371 x=0.5/8760: gosub 100
1372 t$="porn": gosub 40
1373 print "fuck yeah": gosub 30
1374 print "nice": gosub 30
1375 print "sweet": gosub 30: goto 1300
2000 rem bank
2020 t$="bank": gosub 40
2021 m$(1)="withdrawl"
2022 m$(2)="deposit"
2023 m$(3)="view bank account"
2024 m$(4)="rob bank"
2025 m$(5)="leave bank"
2026 m=5: gosub 70
2050 print: input "what would you like to do";i
2051 if i=1 then 2100
2052 if i=2 then 2200
2053 if i=3 then 2300
2054 if i=4 then 2400
2055 if i=5 then 500
2100 rem withdrawl
2120 t$="withdrawl": gosub 40
2121 print "cash in bank:"b
2122 print "cash on hand:"c
2150 print: input "how much";i
2151 if i=0 then 2000
2152 if i>b then 2160
2153 if i<b then b=b-i: c=c+i
2154 print: print "thanks! come again."
2155 gosub 30: goto 500
2160 print: print "you do not have enough funds"
2161 gosub 30: goto 2000
2200 rem deposit
2220 t$="deposit": gosub 40
2221 print "cash on hand:"c
2222 print "cash in bank:"b
2250 print: input "how much";i
2251 if i=0 then 2000
2252 if i>c then 2260
2253 if i<c then b=b+i: c=c-i
2254 print: print "thanks! come again."
2255 gosub 30: goto 500
2260 print: print "you do not have enough funds"
2261 gosub 30: goto 2000
2300 rem view bank acc
2320 t$="balance": gosub 40
2330 print "you have"b"dollars"
2331 print "in your bank account"
2340 gosub 30: goto 2000
2400 rem rob bank
2410 if bfg>0 then 2420
2411 gosub 60: ch=ch+(gun/100)
2412 if ch>58 then 2450
2413 if ch<9 then 7510
2420 t$="bank robbed": gosub 40
2421 bnk=bnk+1: x=(int(rnd(0)*4+2)/5.256e6): gosub 100
2422 cr=int(rnd(0)*1e5)+3e3: c=c+cr
2423 print "you make it out with"cr"dollars"
2424 gosub 30: goto 500
2450 t$="you were caught!": gosub 40
2451 yr=int(rnd(0)*20)+20
2452 print "you are sentenced"yr"years in jail"
2453 brb=int(rnd(0)*3e4)+1e3
2454 gosub 30
2500 rem jail
2520 t$="jail": gosub 40
2521 m$(1)="serve sentence"
2522 m$(2)="bribe guard"
2523 m$(3)="escape"
2524 m$(4)="jeffrey epstein"
2525 m=4: gosub 70
2550 print: input "what now";i
2551 if i=1 then 2600
2552 if i=2 then 2700
2553 if i=3 then 2800
2554 if i=4 then 7530
2600 rem serve sentence
2610 x=yr: gosub 100
2620 t$="sentence served": gosub 40
2630 print "you serve your sentence of"yr"years"
2640 gosub 30: goto 500
2700 rem bribe guard
2720 t$="bribe guard": gosub 40
2730 print "the guard wants"brb"dollars": print
2731 m$(1)="yes"
2732 m$(2)="no"
2733 m=2: gosub 70
2750 print: input "do you pay the bribe";i
2751 if i=1 then 2760
2752 if i=2 then 2500
2760 if c>brb then c=c-brb
2761 if c<brb then 2770
2762 print: print "you pay the bribe"
2763 gosub 30: goto 500
2770 t$="no funds": gosub 40
2771 print: print "you do not have enough funds"
2772 gosub 30: goto 2500
2800 rem escape from jail
2820 t$="escape": gosub 40
2821 print "you attempt to escape..."
2822 gosub 30: gosub 60
2823 if ch<11 then 2830
2824 goto 7510
2830 print: print "and make it out!"
2831 gosub 30: goto 500
3000 rem car shop
3020 t$="carshop": gosub 40
3021 m$(1)="nissan shitbox     ($35,000)"
3022 m$(2)="mystery machine    ($55,000)"
3023 m$(3)="knightrider       ($175,000)"
3024 m$(4)="batmobile       ($1,500,000)"
3025 m$(5)="leave"
3026 m=5: gosub 70
3050 print: input "which car to buy";i
3051 if i=1 then 3100
3052 if i=2 then 3200
3053 if i=3 then 3300
3054 if i=4 then 3400
3055 if i=5 then 500
3100 if c<35000 then 3600
3110 c=c-35000: goto 3500
3200 if c<55000 then 3600
3210 c=c-55000: goto 3500
3300 if c<175000 then 3600
3310 c=c-175000: goto 3500
3400 if c<1.5e6 then 3600
3410 c=c-1.5e6: goto 3500
3500 rem vehicle purchased
3520 t$="vehicle purchased": gosub 40
3530 print "it is yours"
3540 car=car+1
3550 gosub 30: goto 500
3600 rem vehicle not purchased
3620 t$="not enough cash": gosub 40
3630 print "you cannot afford this purchase"
3640 gosub 30: goto 500
4000 rem bar
4020 t$="bar": gosub 40
4021 m$(1)="pick up chick"
4022 m$(2)="drink"
4023 m$(3)="leave"
4024 m=3: gosub 70
4050 print: input "what do you do";i
4051 if i=1 then 4100
4052 if i=2 then 4200
4053 if i=3 then 500
4100 rem pick up chick
4110 x=1/365.25: gosub 100: gosub 60
4111 if ch<6 then 7570
4112 ch=ch-((y-18)/100)+((c/10000)/100)
4113 if ch>80 then 4130
4120 t$="bitch fucked": gosub 40
4121 print "you fuck a bitch": gosub 30
4122 print "nice" 
4123 sex=1+sex: gosub 30: goto 500
4130 t$="dude fucked": gosub 40
4131 print "you wake up with someone else in bed": gosub 30
4132 print "it's a dude": gosub 30
4133 print "you vomit"
4134 gosub 30: goto 500
4200 rem drink
4210 x=1/365.25: gosub 100
4211 x=int(rnd(0)*11)+3: beer=x+beer
4212 g=x+g
4213 if g>22 then 7560
4220 t$="beers chugged": gosub 40
4221 print "you chug"x"beers"
4222 gosub 30: goto 4000
5000 rem your car
5010 if car>0 then 5100
5020 t$="no car": gosub 40
5021 print "you do not have a car"
5022 gosub 30: goto 500
5100 rem car menu
5120 t$="car": gosub 40
5121 m$(1)="mall"
5122 m$(2)="gun shop"
5123 m$(3)="sucicide"
5124 m$(4)="leave"
5125 m=4: gosub 70
5150 print: input "what now";i
5151 if i=1 then 5200
5152 if i=2 then 5300
5153 if i=3 then 7550
5154 if i=4 then 500
5200 rem mall
5220 t$="mall": gosub 40
5221 m$(1)="spencers"
5222 m$(2)="abercrombie"
5223 m$(3)="swarovski"
5224 m$(4)="aladdin's castle"
5225 m$(5)="leave"
5226 m=5: gosub 70
5250 print: input "which place";i
5251 if i=1 then goto 5260
5252 if i=2 then goto 5260
5253 if i=3 then goto 5260
5254 if i=4 then goto 5260
5255 if i=5 then goto 5100
5260 rem closed
5261 t$="closed": gosub 40
5262 print "this store is closed"
5263 print "until further notice"
5264 gosub 30: goto 5200
5300 rem gun shop
5320 t$="gun shop": gosub 40
5321 m$(1)="s.mart boomstick        ($350)"
5322 m$(2)="lawgiver mk. ii       ($3,000)"
5323 m$(3)="endo battle rifle    ($45,000)"
5324 m$(4)="h&k g11             ($230,000)"
5325 m$(5)="bfg 10k           ($1,000,000)"
5326 m$(6)="leave"
5327 m=6: gosub 70
5350 print: input "what do you want to buy";i
5351 if i=1 then goto 5410
5352 if i=2 then goto 5420
5353 if i=3 then goto 5430
5354 if i=4 then goto 5440
5355 if i=5 then goto 5450
5356 if i=6 then goto 5100
5410 if c<350 then 5900
5415 c=c-350: goto 5800
5420 if c<3000 then 5900
5425 c=c-3000: goto 5800
5430 if c<45000 then 5900
5435 c=c-45000: goto 5800
5440 if c<2.3e5 then 5900
5445 c=c-2.3e5: goto 5800
5450 if c<1e6 then 5900
5455 c=c-1e6: bfg=1+bfg: goto 5800
5800 rem gun purchased
5820 t$="gun purchased": gosub 40
5821 print "it is yours"
5830 gun=1+gun: gosub 30: goto 5100
5900 rem gun not purchased
5920 t$="not enough cash": gosub 40
5930 print "you cannot afford this purchase"
5940 gosub 30: goto 5100
6000 rem statistics
6020 t$="statistics": gosub 40
6021 print "age:"int(y*100)/100: print
6022 print "cash on hand:"c: print
6023 print "cash in bank:"b: print
6024 print "total cash:"c+b: print
6025 print "banks robbed:"bnk: print
6026 print "chicks screwed:"sex: print
6027 print "beers chugged:"beer: print
6028 print "number of cars:"car: print
6029 print "guns owned:"gun
6030 gosub 50: goto 500
7000 rem end
7010 sys 64738
7500 rem death cases
7510 rem death by bullets
7511 t$="you died": gosub 40
7512 print "you were shot with"int(rnd(0)*4)+2"bullets and died"
7513 gosub 50: goto 7000
7520 rem death by old age
7521 t$="old as fuck": gosub 40
7522 print "you have died at the old age of"int(y*1)/1
7523 print: print "sucks to be you"
7524 gosub 50: goto 7000
7530 rem death by hanging
7531 t$="you died": gosub 40
7532 print "the convicts craft a makeshift noose"
7533 print "from toilet paper and then hang you"
7534 print "from the ceiling"
7535 gosub 50: goto 7000
7540 rem death by shower
7541 t$="you drowned": gosub 40
7542 print "you were in the shower for"int(rnd(0)*45)+45
7543 print "minutes, fell asleep, and drowned"
7544 gosub 50: goto 7000
7550 rem death by car sucicide
7551 t$="you killed yourself": gosub 40
7552 print "you hop in the car and drive"
7553 print "yourself off of a cliff and die"
7554 gosub 50: goto 7000
7560 rem death by intoxication
7561 t$="alcohol poisoning": gosub 40
7562 print "you drank"g"beers today"
7563 print "you die of too much alcohol"
7564 gosub 30
7565 print: print "you dumb fuck"
7566 gosub 50: goto 7000
7570 rem death by boyfriend
7571 t$="blacked": gosub 40
7572 print "you approach a girl": gosub 30
7573 print "a black guy approaches and"
7574 print "puts a hand on her shoulder...": gosub 30
7575 print "then beats the shit out of you": gosub 30
7576 print: print "you die from your injuries"
7577 gosub 50: goto 7000
